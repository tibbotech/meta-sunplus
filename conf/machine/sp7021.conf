#@TYPE: Machine
#@NAME: sp7021 A7 (primary) CPU
#@DESCRIPTION: Machine configuration for the sp7021 Plus1 series SoC https://tibbo.com/store/plus1.html

# CPU features and optimizations

DEFAULTTUNE ?= "cortexa9hf-neon"
# Yocto branch: < Kirkstone
require conf/machine/include/tune-cortexa9.inc
# Yocto branch: >= Kirkstone
# require conf/machine/include/arm/armv7a/tune-cortexa9.inc

# Bootloaders and kernels
require conf/machine/include/sp-prefs.inc
# Yocto branch: >= Nanbield has SSL 3.0, allowed with kernel 5.10
PREFERRED_VERSION_linux-sp ?= "5.4%"

# will be deprecated soon
SERIAL_CONSOLES ?= "115200;ttyS0"

SOC_B ??= "sp7021-arm5"

ISP_VAR_DTB ?= "sp7021-ltpp3g2revD.dtb"

require conf/machine/include/sp-rrecommends.inc

require conf/machine/include/sp7021-isp.inc

EXTRA_IMAGEDEPENDS += "spmake-native"
EXTRA_IMAGEDEPENDS += "isp-native"
EXTRA_IMAGEDEPENDS += "virtual/bootloader"

#do_image[mcdepends] += "mc:${BB_CURRENT_MC}:${SOC_B}:packagegroup-a926xb:do_deploy"
#do_image[mcdepends] += "mc:${BB_CURRENT_MC}:${SOC_B}:packagegroup-a926fw:do_deploy"
do_image[mcdepends] += "mc:${BB_CURRENT_MC}:${SOC_B}:xboot-nand:do_deploy"
do_image[mcdepends] += "mc:${BB_CURRENT_MC}:${SOC_B}:xboot-emmc:do_deploy"
do_image[mcdepends] += "mc:${BB_CURRENT_MC}:${SOC_B}:xboot-emmc-us:do_deploy"
do_image[mcdepends] += "mc:${BB_CURRENT_MC}:${SOC_B}:xboot-emmc-sd:do_deploy"
do_image[mcdepends] += "mc:${BB_CURRENT_MC}:${SOC_B}:bnoos:do_deploy"

PACKAGE_FEED_ARCHS = "cortexa9hf_neon noarch ${MACHINE}"

BBMULTICONFIG += "${MACHINE} ${SOC_B} qemux86-64"

